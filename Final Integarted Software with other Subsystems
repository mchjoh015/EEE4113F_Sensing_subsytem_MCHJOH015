#include <WiFi.h>
#include <WiFiUdp.h>
#include <ESP32Servo.h>  // Servo library

// WiFi credentials
const char* ssid = "Esp32";
const char* password = "onetwothree";
const char* udpAddress = "172.20.10.5";
const int udpPort = 4210;

// Pins
const int SERVO_SCAN_PIN = 12; // Scanning servo
const int TRIG_PIN = 14;
const int ECHO_PIN = 27;
const int PAN_PIN = 18;        // Pan servo
const int TILT_PIN = 19;       // Tilt servo
const int RELAY_PIN = 26;      // Relay for pump

// Hardware objects
Servo scanServo;
Servo panServo;
Servo tiltServo;
WiFiUDP udp;

// Detection state
long detectedDistance = -1;
int detectedAngle = -1;
bool readyForNewDetection = true;

// Current servo positions
int currentPan = 90;
int currentTilt = 90;

// Servo mounting height (cm)
const float servoHeight = 20.0;

void setup() {
  Serial.begin(115200);
  delay(1000);

  // Connect WiFi
  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);
  Serial.print("\nConnecting");
  while (WiFi.status() != WL_CONNECTED) {
    Serial.print(".");
    delay(1000);
  }
  Serial.println("\nConnected to WiFi");
  Serial.print("IP: ");
  Serial.println(WiFi.localIP());

  // Servo setup
  scanServo.attach(SERVO_SCAN_PIN);
  panServo.setPeriodHertz(50);
  tiltServo.setPeriodHertz(50);
  panServo.attach(PAN_PIN, 500, 2400);
  tiltServo.attach(TILT_PIN, 500, 2400);
  panServo.write(currentPan);      // Neutral pan
  tiltServo.write(currentTilt);    // Neutral tilt

  // Sensor setup
  pinMode(TRIG_PIN, OUTPUT);
  pinMode(ECHO_PIN, INPUT);

  // Relay setup
  pinMode(RELAY_PIN, OUTPUT);
  digitalWrite(RELAY_PIN, LOW); // Pump off initially

  // ===== 20-second scan at startup (pan/tilt inactive) =====
  unsigned long startTime = millis();
  Serial.println("Starting 20-second scan (pan/tilt inactive)");

  while (millis() - startTime < 20000) {
    for (int angle = 0; angle <= 180; angle += 2) {
      scanServo.write(angle);
      delay(200);
      long distance = measureDistance();
      sendUDP(angle, distance);
    }
    for (int angle = 180; angle >= 0; angle -= 2) {
      scanServo.write(angle);
      delay(200);
      long distance = measureDistance();
      sendUDP(angle, distance);
    }
  }

  Serial.println("20-second scan complete. Starting detection...");
}

void loop() {
  for (int angle = 0; angle <= 180; angle++) {
    scanServo.write(angle);
    delay(50);

    long distance = measureDistance();

    if (!readyForNewDetection && detectedDistance < 100 && detectedDistance > 0) {
      respondWithServos();
      return;
    }

    checkAndStoreDetection(angle, distance);
    sendUDP(angle, distance);
  }

  for (int angle = 180; angle >= 0; angle--) {
    scanServo.write(angle);
    delay(50);

    long distance = measureDistance();

    if (!readyForNewDetection && detectedDistance < 100 && detectedDistance > 0) {
      respondWithServos();
      return;
    }

    checkAndStoreDetection(angle, distance);
    sendUDP(angle, distance);
  }
}

long measureDistance() {
  digitalWrite(TRIG_PIN, LOW);
  delayMicroseconds(2);
  digitalWrite(TRIG_PIN, HIGH);
  delayMicroseconds(10);
  digitalWrite(TRIG_PIN, LOW);

  long duration = pulseIn(ECHO_PIN, HIGH, 25000); 
  long distanceCm = duration * 0.0343 / 2;
  if (distanceCm == 0 || distanceCm > 100) {
    distanceCm = 100;
  }
  return distanceCm;
}

void sendUDP(int angle, long distance) {
  String message = String(angle) + "," + String(distance) + ".";
  udp.beginPacket(udpAddress, udpPort);
  udp.print(message);
  udp.endPacket();
  Serial.println(message);
}

void checkAndStoreDetection(int angle, long distance) {
  if (readyForNewDetection && distance < 100) {
    detectedAngle = angle;
    detectedDistance = distance;
    readyForNewDetection = false;
  }
}

void respondWithServos() {
  currentPan = constrain(detectedAngle + 30, 0, 180);
  panServo.write(currentPan);

  float tiltRadians = atan2(detectedDistance, servoHeight);
  float tiltDegrees = degrees(tiltRadians);

  currentTilt = 165 - tiltDegrees;
  currentTilt = constrain(currentTilt, 0, 180);
  tiltServo.write(currentTilt);

  Serial.print("Target detected. Moving to pan: ");
  Serial.print(currentPan);
  Serial.print(", tilt: ");
  Serial.println(currentTilt);

  // === Activate pump ===
  digitalWrite(RELAY_PIN, HIGH);
  Serial.println("Pump ON");
  delay(5000);
  digitalWrite(RELAY_PIN, LOW);
  Serial.println("Pump OFF");

  // Return to neutral
  currentPan = 90;
  currentTilt = 90;
  panServo.write(currentPan);
  tiltServo.write(currentTilt);
  Serial.println("Servos returned to neutral");

  delay(2000);
  readyForNewDetection = true;
}
